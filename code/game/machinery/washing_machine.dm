//dye registry, add dye colors and their resulting output here if you want the sprite to change instead of just the color.
GLOBAL_LIST_INIT(dye_registry, list(
	DYE_REGISTRY_UNDER = list(
		DYE_RED = /obj/item/clothing/under/color/red,
		DYE_ORANGE = /obj/item/clothing/under/color/orange,
		DYE_YELLOW = /obj/item/clothing/under/color/yellow,
		DYE_GREEN = /obj/item/clothing/under/color/green,
		DYE_BLUE = /obj/item/clothing/under/color/blue,
		DYE_PURPLE = /obj/item/clothing/under/color/purple,
		DYE_BLACK = /obj/item/clothing/under/color/black,
		DYE_WHITE = /obj/item/clothing/under/color/white,
		DYE_RAINBOW = /obj/item/clothing/under/color/rainbow,
		DYE_MIME = /obj/item/clothing/under/rank/civilian/mime,
		DYE_CLOWN = /obj/item/clothing/under/rank/civilian/clown,
		DYE_QM = /obj/item/clothing/under/rank/cargo/quartermaster,
		DYE_LAW = /obj/item/clothing/under/suit/black,
		DYE_CAPTAIN = /obj/item/clothing/under/rank/captain,
		DYE_HOP = /obj/item/clothing/under/rank/civilian/head_of_personnel,
		DYE_HOS = /obj/item/clothing/under/rank/security/head_of_security,
		DYE_CE = /obj/item/clothing/under/rank/engineering/chief_engineer,
		DYE_RD = /obj/item/clothing/under/rank/rnd/research_director,
		DYE_CMO = /obj/item/clothing/under/rank/medical/chief_medical_officer,
		DYE_DENIED = /obj/item/clothing/under/color/rainbow/denied,
		DYE_SECURITY = /obj/item/clothing/under/rank/security/officer,
		DYE_SYNDICATE = /obj/item/clothing/under/syndicate,
		DYE_CENTCOM = /obj/item/clothing/under/rank/centcom/commander
	),
	DYE_REGISTRY_JUMPSKIRT = list(
		DYE_RED = /obj/item/clothing/under/color/jumpskirt/red,
		DYE_ORANGE = /obj/item/clothing/under/color/jumpskirt/orange,
		DYE_YELLOW = /obj/item/clothing/under/color/jumpskirt/yellow,
		DYE_GREEN = /obj/item/clothing/under/color/jumpskirt/green,
		DYE_BLUE = /obj/item/clothing/under/color/jumpskirt/blue,
		DYE_PURPLE = /obj/item/clothing/under/color/jumpskirt/purple,
		DYE_BLACK = /obj/item/clothing/under/color/jumpskirt/black,
		DYE_WHITE = /obj/item/clothing/under/color/jumpskirt/white,
		DYE_RAINBOW = /obj/item/clothing/under/color/jumpskirt/rainbow,
		DYE_MIME = /obj/item/clothing/under/rank/civilian/mime/skirt,
		DYE_CLOWN = /obj/item/clothing/under/rank/civilian/clown/sexy,
		DYE_QM = /obj/item/clothing/under/rank/cargo/quartermaster/skirt,
		DYE_LAW = /obj/item/clothing/under/suit/black/skirt,
		DYE_CAPTAIN = /obj/item/clothing/under/rank/captain/skirt,
		DYE_HOP = /obj/item/clothing/under/rank/civilian/head_of_personnel/skirt,
		DYE_HOS = /obj/item/clothing/under/rank/security/head_of_security/skirt,
		DYE_CE = /obj/item/clothing/under/rank/engineering/chief_engineer/skirt,
		DYE_RD = /obj/item/clothing/under/rank/rnd/research_director/skirt,
		DYE_CMO = /obj/item/clothing/under/rank/medical/chief_medical_officer/skirt,
		DYE_DENIED = /obj/item/clothing/under/color/rainbow/denied/skirt,
		DYE_SECURITY = /obj/item/clothing/under/rank/security/officer/skirt,
		DYE_SECURITY = /obj/item/clothing/under/rank/security/officer,
		DYE_SYNDICATE = /obj/item/clothing/under/syndicate,
		DYE_CENTCOM = /obj/item/clothing/under/rank/centcom/commander
	),
	DYE_REGISTRY_SUITS = list(
		DYE_QM = /obj/item/clothing/under/rank/cargo/quartermaster/turtleneck,
		DYE_LAW = /obj/item/clothing/under/rank/civilian/lawyer/black,
		DYE_CAPTAIN = /obj/item/clothing/under/rank/captain/parade,
		DYE_HOS = /obj/item/clothing/under/rank/security/head_of_security/parade
	),
	DYE_REGISTRY_GLOVES = list(
		DYE_RED = /obj/item/clothing/gloves/color/red,
		DYE_ORANGE = /obj/item/clothing/gloves/color/orange,
		DYE_YELLOW = /obj/item/clothing/gloves/color/color_yellow,
		DYE_GREEN = /obj/item/clothing/gloves/color/green,
		DYE_BLUE = /obj/item/clothing/gloves/color/blue,
		DYE_PURPLE = /obj/item/clothing/gloves/color/purple,
		DYE_BLACK = /obj/item/clothing/gloves/color/black,
		DYE_WHITE = /obj/item/clothing/gloves/color/white,
		DYE_RAINBOW = /obj/item/clothing/gloves/color/rainbow,
		DYE_MIME = /obj/item/clothing/gloves/color/white,
		DYE_CLOWN = /obj/item/clothing/gloves/color/rainbow,
		DYE_QM = /obj/item/clothing/gloves/fingerless,
		DYE_CAPTAIN = /obj/item/clothing/gloves/color/captain,
		DYE_HOP = /obj/item/clothing/gloves/color/grey,
		DYE_HOS = /obj/item/clothing/gloves/color/black,
		DYE_CE = /obj/item/clothing/gloves/color/yellow,
		DYE_RD = /obj/item/clothing/gloves/color/grey,
		DYE_CMO = /obj/item/clothing/gloves/color/latex/nitrile,
		DYE_DENIED = /obj/item/clothing/gloves/color/denied,
		DYE_SECURITY = /obj/item/clothing/gloves/color/black,
		DYE_SYNDICATE = /obj/item/clothing/under/syndicate,
		DYE_CENTCOM = /obj/item/clothing/under/rank/centcom/commander
	),
	DYE_REGISTRY_BANDANA = list(
		DYE_RED = /obj/item/clothing/mask/bandana/red,
		DYE_ORANGE = /obj/item/clothing/mask/bandana/orange,
		DYE_YELLOW = /obj/item/clothing/mask/bandana/gold,
		DYE_GREEN = /obj/item/clothing/mask/bandana/green,
		DYE_BLUE = /obj/item/clothing/mask/bandana/blue,
		DYE_PURPLE = /obj/item/clothing/mask/bandana/purple,
		DYE_BLACK = /obj/item/clothing/mask/bandana/black,
		DYE_WHITE = /obj/item/clothing/mask/bandana/white
	),
	DYE_REGISTRY_SNEAKERS = list(
		DYE_RED = /obj/item/clothing/shoes/sneakers/red,
		DYE_ORANGE = /obj/item/clothing/shoes/sneakers/orange,
		DYE_YELLOW = /obj/item/clothing/shoes/sneakers/yellow,
		DYE_GREEN = /obj/item/clothing/shoes/sneakers/green,
		DYE_BLUE = /obj/item/clothing/shoes/sneakers/blue,
		DYE_PURPLE = /obj/item/clothing/shoes/sneakers/purple,
		DYE_BLACK = /obj/item/clothing/shoes/sneakers/black,
		DYE_WHITE = /obj/item/clothing/shoes/sneakers/white,
		DYE_RAINBOW = /obj/item/clothing/shoes/sneakers/rainbow,
		DYE_MIME = /obj/item/clothing/shoes/sneakers/black,
		DYE_QM = /obj/item/clothing/shoes/sneakers/brown,
		DYE_CAPTAIN = /obj/item/clothing/shoes/sneakers/brown,
		DYE_HOP = /obj/item/clothing/shoes/sneakers/brown,
		DYE_CE = /obj/item/clothing/shoes/sneakers/brown,
		DYE_RD = /obj/item/clothing/shoes/sneakers/brown,
		DYE_CMO = /obj/item/clothing/shoes/sneakers/brown,
		DYE_DENIED = /obj/item/clothing/shoes/sneakers/rainbow/denied,
		DYE_SECURITY = /obj/item/clothing/shoes/jackboots
	),

	DYE_REGISTRY_CAP = list(
		DYE_RED = /obj/item/clothing/head/soft/red,
		DYE_ORANGE = /obj/item/clothing/head/soft/orange,
		DYE_YELLOW = /obj/item/clothing/head/soft/yellow,
		DYE_GREEN = /obj/item/clothing/head/soft/green,
		DYE_BLUE = /obj/item/clothing/head/soft/blue,
		DYE_PURPLE = /obj/item/clothing/head/soft/purple,
		DYE_BLACK = /obj/item/clothing/head/soft/black,
		DYE_WHITE = /obj/item/clothing/head/soft,
		DYE_RAINBOW = /obj/item/clothing/head/soft/rainbow,
		DYE_MIME = /obj/item/clothing/head/beret,
		DYE_CLOWN = /obj/item/clothing/head/costume/kitty,
		DYE_QM = /obj/item/clothing/head/soft/cargo,
		DYE_LAW = /obj/item/clothing/head/hats/bowler,
		DYE_CAPTAIN = /obj/item/clothing/head/hats/caphat,
		DYE_HOP = /obj/item/clothing/head/hats/hopcap,
		DYE_HOS = /obj/item/clothing/head/hats/hos,
		DYE_CE = /obj/item/clothing/head/utility/hardhat/white,
		DYE_RD = /obj/item/clothing/head/soft/purple,
		DYE_CMO = /obj/item/clothing/head/soft,
		DYE_DENIED = /obj/item/clothing/head/soft/denied,
		DYE_SECURITY = /obj/item/clothing/head/soft/sec
	),
	DYE_REGISTRY_BERET = list(
		DYE_RAINBOW = /obj/item/clothing/head/beret/rainbow,
		DYE_MIME = /obj/item/clothing/head/beret/mime,
		DYE_CLOWN = /obj/item/clothing/head/beret/clown,
		DYE_QM = /obj/item/clothing/head/beret/cargo,
		DYE_LAW = /obj/item/clothing/head/beret/black,
		DYE_CAPTAIN = /obj/item/clothing/head/caphat/beret,
		DYE_HOS = /obj/item/clothing/head/hats/hos/beret,
		DYE_CE = /obj/item/clothing/head/beret/ce,
		DYE_RD = /obj/item/clothing/head/beret/science,
		DYE_CMO = /obj/item/clothing/head/beret/medical/cmo,
		DYE_SECURITY = /obj/item/clothing/head/beret/sec
	),
	DYE_REGISTRY_FANNYPACK = list(
		DYE_RED = /obj/item/storage/belt/fannypack/red,
		DYE_ORANGE = /obj/item/storage/belt/fannypack/orange,
		DYE_YELLOW = /obj/item/storage/belt/fannypack/yellow,
		DYE_GREEN = /obj/item/storage/belt/fannypack/green,
		DYE_BLUE = /obj/item/storage/belt/fannypack/blue,
		DYE_PURPLE = /obj/item/storage/belt/fannypack/purple,
		DYE_BLACK = /obj/item/storage/belt/fannypack/black,
		DYE_WHITE = /obj/item/storage/belt/fannypack/white,
		DYE_DENIED = /obj/item/storage/belt/fannypack/red
	),
	DYE_REGISTRY_BEDSHEET = list(
		DYE_RED = /obj/item/bedsheet/red,
		DYE_ORANGE = /obj/item/bedsheet/orange,
		DYE_YELLOW = /obj/item/bedsheet/yellow,
		DYE_GREEN = /obj/item/bedsheet/green,
		DYE_BLUE = /obj/item/bedsheet/blue,
		DYE_PURPLE = /obj/item/bedsheet/purple,
		DYE_BLACK = /obj/item/bedsheet/black,
		DYE_WHITE = /obj/item/bedsheet,
		DYE_RAINBOW = /obj/item/bedsheet/rainbow,
		DYE_MIME = /obj/item/bedsheet/mime,
		DYE_CLOWN = /obj/item/bedsheet/clown,
		DYE_QM = /obj/item/bedsheet/qm,
		DYE_LAW = /obj/item/bedsheet/black,
		DYE_CAPTAIN = /obj/item/bedsheet/captain,
		DYE_HOP = /obj/item/bedsheet/hop,
		DYE_HOS = /obj/item/bedsheet/hos,
		DYE_CE = /obj/item/bedsheet/ce,
		DYE_RD = /obj/item/bedsheet/rd,
		DYE_CMO = /obj/item/bedsheet/cmo,
		DYE_COSMIC = /obj/item/bedsheet/cosmos,
		DYE_DENIED = /obj/item/bedsheet/cult
	),
	DYE_REGISTRY_GUN = list(
		DYE_CLOWN = /obj/item/food/grown/banana
	),
	DYE_REGISTRY_BANANA = list(
		DYE_CLOWN = /obj/item/gun/ballistic/shotgun/riot
	),
	DYE_REGISTRY_PEEL = list(
		DYE_CLOWN = /obj/item/caution
	)
))

/obj/machinery/washing_machine
	name = "washing machine"
	desc = "Gets rid of those pesky bloodstains, or your money back!"
	icon = 'icons/obj/machines/washing_machine.dmi'
	icon_state = "wm_1_0"
	density = TRUE
	state_open = TRUE
	var/busy = FALSE
	var/bloody_mess = 0
	var/obj/item/color_source
	var/max_wash_capacity = 5
	var/datum/looping_sound/washing_machine/soundloop

/obj/machinery/washing_machine/Initialize(mapload)
	. = ..()
	soundloop = new(src,  FALSE)

/obj/machinery/washing_machine/Destroy()
	QDEL_NULL(soundloop)
	. = ..()

/obj/machinery/washing_machine/examine(mob/user)
	. = ..()
	if(!busy)
		. += "<span class='notice'><b>Right-click</b> with an empty hand to start a wash cycle.</span>"
	if(bloody_mess)
		. += span_notice("[src] is dirty!")

/obj/machinery/washing_machine/process(delta_time)
	if(!busy)
		animate(src, transform=matrix(), time=2)
		return PROCESS_KILL
	if(anchored)
		if(DT_PROB(2.5, delta_time))
			var/matrix/M = new
			M.Translate(rand(-1, 1), rand(0, 1))
			animate(src, transform=M, time=1)
			animate(transform=matrix(), time=1)
	else
		if(DT_PROB(0.5, delta_time))
			step(src, pick(GLOB.cardinals))
		var/matrix/M = new
		M.Translate(rand(-3, 3), rand(-1, 3))
		animate(src, transform=M, time=2)

/obj/machinery/washing_machine/proc/wash_cycle()
	for(var/X in contents)
		var/atom/movable/AM = X
		AM.wash(CLEAN_WASH)
		AM.machine_wash(src)
	if(color_source)
		color_source = null
		for(var/obj/item/book/manual/book in contents)
			qdel(book)
		for(var/obj/item/paper/paper in contents)
			qdel(paper)
	busy = FALSE
	update_icon()
	soundloop.stop()

/obj/item/proc/dye_item(dye_color, dye_key_override)
	var/dye_key_selector = dye_key_override ? dye_key_override : dying_key
	if(undyeable)
		return FALSE
	if(!dye_key_selector)
		return FALSE
	if(!GLOB.dye_registry[dye_key_selector])
		log_runtime("Item just tried to be dyed with an invalid registry key: [dye_key_selector]")
		return FALSE
	var/obj/item/target_type = GLOB.dye_registry[dye_key_selector][dye_color]
	if(!target_type)
		return FALSE
	appearance_change(target_type)
	desc = "[initial(target_type.desc)] The colors look a little dodgy."
	return target_type //successfully "appearance copy" dyed something; returns the target type as a hacky way of extending

/obj/item/proc/appearance_change(var/obj/item/target_type)
	if(initial(target_type.greyscale_config) && initial(target_type.greyscale_colors))
		set_greyscale(
			colors=initial(target_type.greyscale_colors),
			new_config=initial(target_type.greyscale_config),
			new_worn_config=initial(target_type.greyscale_config_worn),
			new_inhand_left=initial(target_type.greyscale_config_inhand_left),
			new_inhand_right=initial(target_type.greyscale_config_inhand_right)
		)
	else
		icon = initial(target_type.icon)
		lefthand_file = initial(target_type.lefthand_file)
		righthand_file = initial(target_type.righthand_file)
		worn_icon = initial(target_type.worn_icon)

	add_atom_colour(initial(target_type.color), FIXED_COLOUR_PRIORITY)
	icon_state = initial(target_type.icon_state)
	item_state = initial(target_type.item_state)
	worn_icon_state = initial(target_type.worn_icon_state)
	inhand_x_dimension = initial(target_type.inhand_x_dimension)
	inhand_y_dimension = initial(target_type.inhand_y_dimension)
	name = initial(target_type.name)

//what happens to this object when washed inside a washing machine
/atom/movable/proc/machine_wash(obj/machinery/washing_machine/WM)
	return

/obj/item/clothing/suit/hooded/ian_costume/machine_wash(obj/machinery/washing_machine/WM)
	new /obj/item/food/meat/slab/corgi(loc)
	qdel(src)

/obj/item/clothing/head/mob_holder/machine_wash(obj/machinery/washing_machine/WM)
	..()
	held_mob.machine_wash(WM)
	qdel(src)

/mob/living/simple_animal/pet/machine_wash(obj/machinery/washing_machine/WM)
	WM.bloody_mess = TRUE
	investigate_log("has been gibbed by a washing machine.", INVESTIGATE_DEATHS)
	gib()

/mob/living/basic/pet/machine_wash(obj/machinery/washing_machine/washer)
	washer.bloody_mess = TRUE
	gib()

/obj/item/machine_wash(obj/machinery/washing_machine/WM)
	remove_atom_colour(WASHABLE_COLOUR_PRIORITY)
	if(WM.color_source)
		dye_item(WM.color_source.dye_color)
		return
	appearance_change(src)
	desc = initial(desc)

/obj/item/gun/energy/laser/practice/dye_item(dye_color, dye_key)
	. = ..()
	if(.)
		cut_overlays()

/obj/item/clothing/under/dye_item(dye_color, dye_key)
	. = ..()
	if(.)
		var/obj/item/clothing/under/U = .
		can_adjust = initial(U.can_adjust)
		if(!can_adjust && adjusted) //we deadjust the uniform if it's now unadjustable
			toggle_jumpsuit_adjust()

/obj/item/clothing/head/soft/dye_item(dye_color, dye_key)
	var/list/dyes = list(DYE_MIME, DYE_CLOWN, DYE_LAW, DYE_CAPTAIN, DYE_HOP, DYE_HOS, DYE_CE)
	. = ..()
	if(.)
		if(dye_color in dyes)
			flippable = FALSE //Making hats unflippable so it doesn't break the illusion of the dye if the result isn't a flippable hat
		else
			flippable = TRUE

/obj/item/clothing/glasses/machine_wash(obj/machinery/washing_machine/WM)
	if(HAS_TRAIT_FROM(src, TRAIT_SPRAYPAINTED, CRAYON_TRAIT))
		flash_protect -= 1
		tint -= 2
		REMOVE_TRAIT(src, TRAIT_SPRAYPAINTED, CRAYON_TRAIT)
	..()

/obj/item/clothing/under/machine_wash(obj/machinery/washing_machine/WM)
	freshly_laundered = TRUE
	addtimer(VARSET_CALLBACK(src, freshly_laundered, FALSE), 5 MINUTES, TIMER_UNIQUE | TIMER_OVERRIDE)
	..()

/obj/item/clothing/shoes/sneakers/machine_wash(obj/machinery/washing_machine/WM)
	if(chained)
		chained = 0
		slowdown = SHOES_SLOWDOWN
		new /obj/item/restraints/handcuffs(loc)
	..()

/obj/machinery/washing_machine/relaymove(mob/living/user, direction)
	container_resist(user)

/obj/machinery/washing_machine/container_resist(mob/living/user)
	if(!busy)
		add_fingerprint(user)
		open_machine()



/obj/machinery/washing_machine/update_icon()
	cut_overlays()
	if(busy)
		icon_state = "wm_running_[bloody_mess]"
	else if(bloody_mess)
		icon_state = "wm_[state_open]_blood"
	else
		var/full = contents.len ? 1 : 0
		icon_state = "wm_[state_open]_[full]"
	if(panel_open)
		add_overlay("wm_panel")

/obj/machinery/washing_machine/attackby(obj/item/W, mob/living/user, params)
	if(panel_open && !busy && default_unfasten_wrench(user, W))
		return

	if(default_deconstruction_screwdriver(user, null, null, W))
		update_icon()
		return

	else if(!user.combat_mode)

		if (!state_open)
			to_chat(user, span_warning("Open the door first!"))
			return TRUE
		else if(bloody_mess)
			if(istype(W, /obj/item/reagent_containers/spray))
				var/obj/item/reagent_containers/spray/clean_spray = W
				if(clean_spray.reagents.has_reagent(/datum/reagent/space_cleaner, clean_spray.amount_per_transfer_from_this))
					clean_spray.reagents.remove_reagent(/datum/reagent/space_cleaner, clean_spray.amount_per_transfer_from_this,1)
					playsound(loc, 'sound/effects/spray3.ogg', 50, 1, -6)
					user.visible_message("[user] has cleaned \the [src].", span_notice("You clean \the [src]."))
					bloody_mess = 0
					update_icon()
				else
					to_chat(user, span_warning("You need more space cleaner!"))
				return TRUE

			else if(istype(W, /obj/item/soap) || istype(W, /obj/item/reagent_containers/cup/rag))
				var/cleanspeed = 50
				if(istype(W, /obj/item/soap))
					var/obj/item/soap/used_soap = W
					cleanspeed = used_soap.cleanspeed
				user.visible_message("[user] starts to clean \the [src].", span_notice("You start to clean \the [src]..."))
				if(do_after(user, cleanspeed, target = src))
					user.visible_message("[user] has cleaned \the [src].", span_notice("You clean \the [src]."))
					bloody_mess = 0
					update_icon()
				return TRUE

		if(bloody_mess)
			to_chat(user, span_warning("[src] must be cleaned up first."))
			return TRUE

		if(contents.len >= max_wash_capacity)
			to_chat(user, span_warning("The washing machine is full!"))
			return TRUE

		if(!user.transferItemToLoc(W, src))
			to_chat(user, span_warning("\The [W] is stuck to your hand, you cannot put it in the washing machine!"))
			return TRUE

		if(W.dye_color)
			color_source = W
		update_icon()

	else
		return ..()

/obj/machinery/washing_machine/attack_hand(mob/living/user)
	. = ..()
	if(.)
		return
	if(busy)
		to_chat(user, span_warning("[src] is busy."))
		return

	if(user.pulling && isliving(user.pulling))
		var/mob/living/L = user.pulling
		if(L.buckled || L.has_buckled_mobs())
			return
		if(state_open)
			if(istype(L, /mob/living/simple_animal/pet) || istype(L, /mob/living/basic/pet))
				L.forceMove(src)
				update_appearance()
		return

	if(!state_open)
		playsound(get_turf(src), 'sound/items/deconstruct.ogg', 50, 1)
		open_machine()
	else
		state_open = FALSE //close the door
		playsound(get_turf(src), 'sound/items/deconstruct.ogg', 50, 1)
		update_appearance()

/obj/machinery/washing_machine/attack_hand_secondary(mob/user, modifiers)
	. = ..()
	if(. == SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN)
		return

	if(!user.canUseTopic(src, !issilicon(user)))
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	if(busy)
		to_chat(user, "<span class='warning'>[src] is busy!</span>")
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	if(state_open)
		to_chat(user, "<span class='warning'>Close the door first!</span>")
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	if(bloody_mess)
		to_chat(user, "<span class='warning'>[src] must be cleaned up first!</span>")
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	//state_open = FALSE //close the door
	busy = TRUE
	update_appearance()
	bloody_mess = FALSE
	addtimer(CALLBACK(src, PROC_REF(wash_cycle)), 20 SECONDS)
	soundloop.start()
	START_PROCESSING(SSfastprocess, src)
	return SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN

/obj/machinery/washing_machine/attack_ai_secondary(mob/user, modifiers)
	return attack_hand_secondary(user, modifiers)

/obj/machinery/washing_machine/deconstruct(disassembled = TRUE)
	if (!(flags_1 & NODECONSTRUCT_1))
		new /obj/item/stack/sheet/iron(drop_location(), 2)
	qdel(src)

/obj/machinery/washing_machine/open_machine(drop = 1)
	..()
	set_density(TRUE) //because machinery/open_machine() sets it to FALSE
	color_source = null
