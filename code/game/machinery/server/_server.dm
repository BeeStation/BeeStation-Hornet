/obj/machinery/server
	name = "Server"
	desc = "You should not be seeing this."
	icon = 'icons/obj/machines/telecomms.dmi'
	icon_state = "message_server_off"

	var/efficiency = 1 // 100%
	var/overheated_temp = T0C + 100 // 100C - temperature at which the server stops working

	var/temperature = T20C // current temperature
	var/heat_capacity = 2500 // Used for auxmos heat transfer

	var/heat_generation = 0 // How much heat is generated by the server

/obj/machinery/server/Initialize()
	. = ..()
	SSair.start_processing_machine(src)
	return INITIALIZE_HINT_LATELOAD

/obj/machinery/server/examine(mob/user)
	. = ..()
	. += "The status display reads: [round(efficiency * 100, 2)]% efficiency."


/obj/machinery/server/process_atmos()
	calculate_temperature()
	if(temperature > overheated_temp)
		efficiency = 0
		set_machine_stat(machine_stat | OVERHEATED)
	else
		set_machine_stat(machine_stat & ~OVERHEATED)
		efficiency = clamp(1 - ((temperature - T20C) / (overheated_temp - T20C)), 0, 1)
		temperature += heat_generation / heat_capacity
		heat_generation = 0

/obj/machinery/server/use_power(amount, chan)
	. = ..()
	heat_generation += amount * 1000

/obj/machinery/server/proc/calculate_temperature()
	var/turf/turf = get_turf(src)
	if(!turf)
		return FALSE
	var/datum/gas_mixture/environment = turf.return_air()

	temperature = environment.temperature_share(null, OPEN_HEAT_TRANSFER_COEFFICIENT, temperature, heat_capacity)
	// Debug output
	var/efficiency_status = efficiency ? "Efficiency: [efficiency]" : "OVERHEATED"
	debug_world("[src] temperature: [temperature]K, [efficiency_status]")
