// Server component
// will generate a heat based on the power usage of the machine
// use only with /obj/machinery



/datum/component/server

	var/efficiency = 1 // 0 to 1 range

	var/temperature = T20C // current temperature
	var/overheated_temp = T0C + 100 // 100C - temperature at which the server stops working
	var/heat_capacity = 2500 // Used for auxmos heat transfer

	var/heat_generation = 0 // heat generated by the machine

/datum/component/server/Initialize()
	if(!ismachinery(parent)) // currently only compatible with machinery
		return COMPONENT_INCOMPATIBLE
	RegisterSignal(parent, COMSIG_MACHINERY_POWER_USED, PROC_REF(ParentPowerUsed))
	START_PROCESSING(SSfastprocess, src)

/datum/component/server/proc/ParentPowerUsed(source, amount, chan)
	heat_generation += amount * 1000

/datum/component/server/process(delta_time)
	var/obj/machinery/parent_machine = parent
	calculate_temperature()
	if(temperature > overheated_temp)
		efficiency = 0
		parent_machine.set_machine_stat(parent_machine.machine_stat | OVERHEATED)
	else
		parent_machine.set_machine_stat(parent_machine.machine_stat & ~OVERHEATED)
		efficiency = clamp(1 - ((temperature - T20C) / (overheated_temp - T20C)), 0, 1)
		temperature += heat_generation / heat_capacity
		heat_generation = 0

/datum/component/server/proc/calculate_temperature()
	var/turf/turf = get_turf(parent)
	if(!turf)
		return FALSE
	var/datum/gas_mixture/environment = turf.return_air()

	temperature = environment.temperature_share(null, OPEN_HEAT_TRANSFER_COEFFICIENT, temperature, heat_capacity)
	// Debug output
	var/efficiency_status = efficiency ? "Efficiency: [efficiency]" : "OVERHEATED"
	var/obj/par = parent
	to_chat(world, "[src] ([ADMIN_JMP(par)]) temperature: [temperature]K, [efficiency_status]")
